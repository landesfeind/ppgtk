
%h{
#include <gtk/gtk.h>

#include <libintl.h>
#define _(x) gettext(x)
%}

class PPGtk:Timedialog from Gtk:Dialog {


	private Gtk:Adjustment *adj_hours = { (GtkAdjustment *)gtk_adjustment_new(0, 0, G_MAXINT, 1, 5, 0) };
	private Gtk:Adjustment *adj_minutes = { (GtkAdjustment *)gtk_adjustment_new(45, 0, G_MAXINT, 1, 5, 0) };

	constructor(self){
		GtkBox *ca = (GtkBox *)gtk_dialog_get_content_area(GTK_DIALOG(self));
		GtkBox *b = (GtkBox *)gtk_box_new(GTK_ORIENTATION_HORIZONTAL, 5);

		gtk_box_pack_start(ca, gtk_label_new(_("Hours : Minutes")), TRUE, TRUE, 5);
		gtk_box_pack_start(ca, GTK_WIDGET(b), TRUE, TRUE, 5);
		gtk_box_pack_start(b, gtk_spin_button_new(selfp->adj_hours, 1, 0)  , TRUE, TRUE, 5);
		gtk_box_pack_start(b, gtk_spin_button_new(selfp->adj_minutes, 1, 0), TRUE, TRUE, 5);

		gtk_dialog_add_button(GTK_DIALOG(self), _("Cancel"), GTK_RESPONSE_CANCEL);
		gtk_dialog_add_button(GTK_DIALOG(self), _("Accept"), GTK_RESPONSE_ACCEPT);

		gtk_dialog_set_default_response(GTK_DIALOG(self), GTK_RESPONSE_ACCEPT);
		gtk_widget_show_all(GTK_WIDGET(self));
	}


	public PPGtk:Timedialog *new(Gtk:Window *parent (check null type)) onerror NULL {
		return GET_NEW_VARG(
				"transient-for", parent, 
				"modal", TRUE,
				"title", _("Time"),
				NULL);
	}

	public gint get_seconds(self){
		return 60 * (gint)gtk_adjustment_get_value(selfp->adj_minutes) + 3600 * (gint)gtk_adjustment_get_value(selfp->adj_hours);
	}

	public void set_seconds(self, gint seconds (check > 0)){
		gtk_adjustment_set_value(selfp->adj_minutes, 0);
		gtk_adjustment_set_value(selfp->adj_hours, 0);

		if( seconds < 60 ){
			return;
		}
		seconds /= 60;

		gtk_adjustment_set_value(selfp->adj_minutes, seconds % 60);
		seconds /= 60;

		gtk_adjustment_set_value(selfp->adj_hours, seconds);
	}
}
